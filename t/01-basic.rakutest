use Test;
use SBOM::CycloneDX;
use SBOM::enums;

plan 1 + 2 * 5;

is LicenseId("0BSD").url, "https://spdx.org/licenses/0BSD.json",
  'did we get the correct URL for BSD';

my constant $format = "CycloneDX";
my constant $spec   = "1.6";

my %minimal =
  bomFormat   => $format,
  specVersion => $spec,
;

for
  "minimal", %minimal,
  "Hash",    SBOM::CycloneDX.Hash
-> $name, %args {
    my $sbom := SBOM::CycloneDX.new(|%args);
    isa-ok $sbom, SBOM::CycloneDX, "Ok object with $name";

    is $sbom.bomFormat,   $format, "$name: is the bomFormat correct";
    is $sbom.specVersion, $spec,   "$name: is the specVersion correct";

    is-deeply $sbom.version, 1, "$name: is the version correct";

    is $sbom.JSON, qq:to/JSON/.chomp, 'is the JSON ok';
\{
  "\$schema": "http://cyclonedx.org/schema/bom-$spec.schema.json",
  "bomFormat": "$format",
  "specVersion": "$spec",
  "version": 1
}
JSON
}

# vim: expandtab shiftwidth=4
